* Cumulative Grammar Definitions

As of §10.3 "Function Declarations"
the current grammar definition is:

#+begin_src text
  program         → declaration* EOF ;

  declaration     → varDecl
                  | funDecl
                  | statement
                  ;

  statement       → exprStmt
                  | printStmt
                  | block
                  | ifStmt
                  | whileStmt
                  | forStmt
                  | breakStmt
                  ;

  funDecl         → "fun" function ;
  function        → IDENTIFIER "(" parameters? ")" block ;
  parameters      → IDENTIFIER ( "," IDENTIFIER )* ;

  # Not encoded in grammar: "break" must only exist
  # within the body of a "for" or "while" statement.

  breakStmt       → "break" ";"

  forStmt         → "for" "(" ( varDecl | exprStmt | ";" )
                     expression? ";" expression? ")" statement ;

  ifStmt          → "if" "(" expression ")" statement
                  ( "else" statement )? ;

  whileStmt       → "while" "(" expression ")" statement ;

  block           → "{" declaration* "}" ;

  varDecl         → "var" IDENTIFIER ( "=" expression )? ";" ;
  exprStmt        → expression ";" ;
  printStmt       → "print" expression ";" ;

  expression      → assignment

  assignment      → IDENTIFIER = assignment
                  | logic_or
                  ;

  logic_or        → logic_and ( "or" logic_and )* ;
  logic_and       → equality ( "and" equality )* ;

  equality        → comparison ( ( "!=" | "==" ) comparison )* ;
  comparison      → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;
  term            → factor ( ( "-" | "+" ) factor )* ;
  factor          → unary ( ( "/" | "*" ) unary )* ;

  unary           → ( "!" | "-" ) unary
                  | call
                  ;

  call            → primary ( "(" arguments? ")" )* ;
  arguments       → expression ( "," expression )* ;

  primary         → "true" | "false" | "nil"
                  | "(" expression ")"
                  | STRING
                  | NUMBER
                  ;
#+end_src
